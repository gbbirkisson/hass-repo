# Default architecture for local testing
ARCH := "amd64"
BASE_IMAGE := "ghcr.io/home-assistant/" + ARCH + "-base:3.23"

# Extract versions from build.yaml for local build
CLAUDE_VERSION := `grep CLAUDE_VERSION build.yaml | awk '{print $2}' | tr -d '"'`

# Build the Docker image locally
build:
    docker build \
        --build-arg BUILD_FROM={{BASE_IMAGE}} \
        --build-arg CLAUDE_VERSION={{CLAUDE_VERSION}} \
        -t local-claude-addon .

# Run the container with persistence and port mapping
# Maps local ./data folder to /data inside container
run:
    mkdir -p data
    if [ ! -f data/options.json ]; then \
        echo '{"startup_script": "echo Hello from startup script!"}' > data/options.json; \
    fi
    docker run \
        --rm \
        -it \
        --name claude-addon \
        -p 7681:7681 \
        -v {{invocation_directory()}}/data:/data \
        local-claude-addon

# Clean up local data directory
clean:
    sudo rm -rf data
